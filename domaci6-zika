MODULE Module1
	CONST robtarget Start:=[[610.2146997,0,619.5],[0.5,0,0.866025404,0],[0,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_20:=[[444.664508172,417.894071389,619.5],[0.46485236,-0.318962146,0.805147905,0.184152881],[0,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_30:=[[646.859598684,607.916274726,486.215627807],[0.463218633,-0.319334937,0.80608893,0.183505674],[0,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_40:=[[834.992620097,331.791300094,337.214023392],[0.410601959,-0.170772614,0.892225551,0.078589511],[0,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_50:=[[748.172362807,-497.530157847,337.214023392],[0.400187576,0.262742337,0.869595414,-0.120913953],[-1,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_60:=[[688.31645704,-457.726337598,628.954501803],[0.535362348,0.239767985,0.793557457,-0.16175609],[-1,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
	CONST robtarget Target_70:=[[758.225788822,-119.120781735,893.301487274],[0.744507043,0.051767543,0.663061627,-0.058126272],[-1,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
  
    VAR robtarget point:=[[610.2146997,0,619.5],[0.5,0,0.866025404,0],[0,0,0,0],[9E9,9E9,9E9,9E9,9E9,9E9]];
    VAR intnum forceStopVect;
    VAR intnum startVector;
    VAR intnum speedUpVect;
    VAR intnum speedDownVect;
    VAR intnum blinkVector;
    VAR intnum velocity_procent := 25;
    VAR num velocity := 1;
    VAR bool fastest_flag := FALSE;
    VAR num fastest_counter := 0;
    
PROC Path_10()
	MoveL Start,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_20,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_30,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_40,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_50,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_60,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_70,v1000,z100,Hvacher_1\WObj:=wobj0;
ENDPROC

PROC Path_20()
	MoveL Target_60,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_50,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_40,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_30,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_20,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Start,v1000,z100,Hvacher_1\WObj:=wobj0;
	MoveL Target_70,v1000,z100,Hvacher_1\WObj:=wobj0;
ENDPROC

TRAP forceStopTrap
    StopMove;
    StorePath;
    point := CRobT();
    MoveL Start,v1000,z100,Hvacher_1\WObj:=wobj0;
    ISleep blinkVector;     !iskljuciti blink
ENDTRAP

TRAP StartTrap
    MoveJ point,v1000,z100,Hvacher_1\WObj:=wobj0;
    RestoPath;
    StartMove;
    IWatch blinkVector;   !pali kad se robot startuje
ENDTRAP

TRAP speedUpTrap
    velocity:=velocity+1;
    IF velocity > 4 THEN
        velocity := 4;
    ENDIF
    
    velocity_procent := velocity_procent + 25;
    IF velocity_procent >= 100 THEN
        velocity_procent := 100;
        fastest_flag := TRUE;
    ENDIF
    
    SpeedRefresh velocity_procent;
    speedUpVect := 0; !RESET FLAG-a
    velocity_disp; 
ENDTRAP

TRAP slowDownTrap 
    velocity:=velocity-1;
    
    IF velocity < 0 THEN
        velocity := 0;
    ENDIF
    
    velocity_procent := velocity_procent - 25;
    IF velocity_procent < 0 THEN
        velocity_procent := 0;
    ENDIF
    
    SpeedRefresh velocity_procent;
    speedDownVect := 0; !RESET FLAG-a
    velocity_disp; 
ENDTRAP

TRAP blink
    InvertDO DO10_11;
    
    IF fastest_flag = TRUE THEN
        fastest_counter := fastest_counter + 1;
    ENDIF
    IF fastest_counter = 20 THEN
        velocity := velocity - 1;
        SpeedRefresh 75;
        fastest_counter := 0;
        fastest_flag := FALSE;
        velocity_disp;
    ENDIF
    
ENDTRAP
PROC velocity_disp()
    TEST velocity
    CASE 1:
    SetDO DO10_9,0;
    SetDO DO10_10,0;   
    CASE 2:
    SetDO DO10_9,0;
    SetDO DO10_10,1;   
    CASE 3:
    SetDO DO10_9,1;
    SetDO DO10_10,0;   
    CASE 4:
    SetDO DO10_9,1;
    SetDO DO10_10,1;   
    ENDTEST
ENDPROC
PROC main()
    CONNECT forceStopVect WITH forceStopTrap;
    CONNECT startVector WITH StartTrap;
    CONNECT speedUpVect WITH speedUpTrap;
    CONNECT speedDownVect WITH slowDownTrap;
    CONNECT blinkVector WITH blink;

    ISignalDI DI10_11,1,forceStopVect;
    ISignalDI DI10_11,0,startVector;
    ISignalDI DI10_9,1,speedUpVect;
    ISignalDI DI10_10,1,speedDownVect;
    ITimer 0.5,blinkVector;
    
    SpeedRefresh velocity_procent;
    velocity_disp;
    
    WHILE TRUE DO 
        IF DI10_12 = 1 THEN
            Path_10;
        ELSE 
            Path_20;
        ENDIF
    ENDWHILE
ENDPROC

ENDMODULE
